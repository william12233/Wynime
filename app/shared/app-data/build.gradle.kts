/*
 * Copyright (C) 2024-2026 OpenAni and contributors.
 *
 * 此源代码的使用受 GNU AFFERO GENERAL PUBLIC LICENSE version 3 许可证的约束, 可以在以下链接找到该许可证.
 * Use of this source code is governed by the GNU AGPLv3 license, which can be found at the following link.
 *
 * https://github.com/open-ani/ani/blob/main/LICENSE
 */

plugins {
    id("ani.kmp-compose")
    alias(libs.plugins.kotlin.plugin.serialization)

    // alias(libs.plugins.kotlinx.atomicfu)
    alias(libs.plugins.kotlin.parcelize)

    alias(libs.plugins.google.devtools.ksp)
    alias(libs.plugins.androidx.room)
    idea
}

kotlin {
    android {
        namespace = "me.him188.ani.app.data"
    }
    sourceSets.commonMain.dependencies {
        implementation(projects.app.shared.appPlatform)
        implementation(projects.app.shared.appLang)
        implementation(projects.utils.intellijAnnotations)
        implementation(libs.compose.components.resources)
        api(projects.app.shared.videoPlayer.videoPlayerApi)
        api(libs.mediamp.api)
        api(libs.mediamp.test)
        api(libs.mediamp.source.ktxio)
        implementation(libs.kotlinx.serialization.json.io)
        api(libs.kotlinx.coroutines.core)
        api(libs.kotlinx.serialization.core)
        api(libs.kotlinx.collections.immutable)
        implementation(libs.kotlinx.serialization.json)
        implementation(projects.utils.io)
        implementation(projects.utils.coroutines)
        api(projects.utils.xml)
        api(projects.utils.coroutines)
        api(projects.client)
        api(projects.utils.ipParser)
        api(projects.utils.jsonpath)
        api(projects.utils.httpDownloader)
        api(projects.utils.serialization)
        api(projects.source.pluginApi)

        api(libs.datastore.core) // Data Persistence
        api(libs.datastore.preferences.core) // Preferences
        api(libs.androidx.room.runtime)
        api(libs.androidx.room.paging)
        api(libs.sqlite.bundled)

        api(projects.datasource.datasourceApi)
        api(projects.datasource.datasourceCore)
        api(projects.datasource.bangumi)
        api(projects.datasource.jellyfin)
        api(projects.datasource.ikaros)

        api(libs.paging.common)

        implementation(libs.koin.core)
        implementation(libs.atomicfu)
        implementation(libs.ktor.network) // HLS 本地代理 (iOS)
    }
    sourceSets.commonTest.dependencies {
        implementation(projects.utils.uiTesting)
        implementation(projects.utils.androidxLifecycleRuntimeTesting)
        implementation(libs.ktor.client.mock)
        implementation(libs.turbine)
        implementation(kotlin("reflect"))
    }
    sourceSets.getByName("jvmTest").dependencies {
        implementation(libs.slf4j.simple)
    }
    sourceSets.desktopMain {
        dependencies {
            implementation(libs.onnxruntime)
            // 判断的是构建主机, 所以 Windows ARM64 包必须在 ARM64 机器上原生构建, 无法从 x64 交叉打包.
            if (getOs() == Os.Windows && getArch() == Arch.AARCH64) {
                // AndroidX sqlite-bundled-jvm 没有 Windows ARM64 native 库, 这里补上本机编译的 sqliteJni.dll.
                // 详见 ci-helper/sqlite-woa64/build.gradle.kts 的头注释
                runtimeOnly(projects.ciHelper.sqliteWoa64)
            }
        }
    }
    sourceSets.getByName("androidDeviceTest").dependencies {
        // 用真实 ExoPlayer 验证 HLS 本地代理 (ExoPlayerHlsProxyDeviceTest)
        implementation(libs.androidx.media3.exoplayer)
        implementation(libs.androidx.media3.exoplayer.hls)
    }
    sourceSets.desktopTest {
        // 与 Android 设备测试共用测试素材 (验证码样本, HLS 夹具等)
        resources.srcDir("src/androidDeviceTest/assets")
        dependencies {
            implementation("androidx.room:room-testing:${libs.versions.room.get()}")
        }
    }
    sourceSets.androidMain.dependencies {
        implementation(libs.androidx.browser)
        implementation(libs.onnxruntime.android)
        api(libs.datastore) // PlatformDataStoreManagerAndroid (data/persistent, 自 :app:shared 搬迁)
        api(libs.datastore.preferences)
        api(libs.androidx.lifecycle.runtime.ktx)
        api(libs.androidx.lifecycle.service)
        api(libs.androidx.lifecycle.process)
    }
    sourceSets.nativeMain.dependencies {
        implementation(libs.stately.common) // fixes koin bug
        implementation(libs.kotlinx.io.okio)
    }
}

compose.resources {
    packageOfResClass = "me.him188.ani.app.data"
    generateResClass = always
}

// 两个都要, 不是重复配置, 删任何一个都会坏:
// - room {} 负责 Android 侧, 并注册 copyRoomSchemas* (把 schema 拷进 androidTest assets 给 MigrationTestHelper 用);
//   但它没把 room.schemaLocation 传给 KMP 的 desktop / iOS 那几个 KSP task.
// - ksp {} 的 arg 对所有 KSP task 生效, 补上 room {} 没覆盖到的 target.
// 少了下面这段, kspKotlinDesktop 会报 "Schema import directory was not provided" 而失败 (自动迁移读不到旧 schema).
// 两者同时存在不冲突, Room 插件不会因此报错.
room {
    schemaDirectory("$projectDir/schemas")
}

ksp {
    arg("room.schemaLocation", "$projectDir/schemas")
}

dependencies {
    kspDesktop(libs.androidx.room.compiler)
    kspAndroid(libs.androidx.room.compiler)
    if (enableIos) {
        add("kspIosArm64", libs.androidx.room.compiler)
        add("kspIosSimulatorArm64", libs.androidx.room.compiler)
    }
}
